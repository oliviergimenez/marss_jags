
model {  

   # Estimate the initial state vector of population abundances
   for(i in 1:nSpecies) {
      X[i,1] ~ dnorm(0,1) # weakly informative normal prior 
      xknot[i] <- X[i,1]
   }

   # B matrix of interactions
   B[1, 1] <- alpha[1]
   B[1, 2] <- alpha[2]
   B[1, 3] <- alpha[3]
   B[1, 4] <- alpha[4]
   B[2, 1] <- alpha[5]
   B[2, 2] <- alpha[6]
   B[2, 3] <- alpha[7]
   B[2, 4] <- alpha[8]
   B[3, 1] <- alpha[9]
   B[3, 2] <- alpha[10]
   B[3, 3] <- alpha[11]
   B[3, 4] <- alpha[12]
   B[4, 1] <- alpha[13]
   B[4, 2] <- alpha[14]
   B[4, 3] <- alpha[15]
   B[4, 4] <- alpha[16]
   for (k in 1:16){
   alpha[k] ~ dunif(-1, 1)
   }

   # Autoregressive process
   for(t in 2:nYears) {
      for(i in 1:nSpecies) {
         predX[i,t] <- inprod(B[i,], X[,t-1])
         X[i,t] ~ dnorm(predX[i,t], tauQ[species[i]])
      }
   }
	 tauQ[1] ~ dscaled.gamma(1, 10)
	 tauQ[2] ~ dscaled.gamma(1, 10)
   Qp <- 1 / tauQ[1]
   Qd <- 1 / tauQ[2]

   # Observation model
   for(t in 1:nYears) {
     for(i in 1:nSpecies) {
       counts[i,t] ~ dnorm(X[i,t], tauR[species[i]])
     }
   }
	 tauR[1] ~ dscaled.gamma(1, 10)
	 tauR[2] ~ dscaled.gamma(1, 10)
   Rp <- 1 / tauR[1]
   Rd <- 1 / tauR[2]

}  

